plugins {
  id 'java'
  id 'org.springframework.boot' version '3.3.4'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.graalvm.buildtools.native' version '0.10.3'
}

apply plugin: 'java'

group = 'org.springframework.samples'
version = '3.3.0'

java {
  sourceCompatibility = JavaVersion.VERSION_17
}

repositories {
  mavenCentral()
}

ext.webjarsFontawesomeVersion = "4.7.0"
ext.webjarsBootstrapVersion = "5.3.8"
ext.hibernateVersion = "6.6.4.Final"

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-cache'
  implementation "org.hibernate:hibernate-core:${hibernateVersion}"
  implementation('org.springframework.boot:spring-boot-starter-data-jpa') {
        exclude group: 'org.hibernate', module: 'hibernate-core'
  }
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'javax.cache:cache-api'
  implementation 'jakarta.xml.bind:jakarta.xml.bind-api'
  runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'
  runtimeOnly "org.webjars.npm:bootstrap:${webjarsBootstrapVersion}"
  runtimeOnly "org.webjars.npm:font-awesome:${webjarsFontawesomeVersion}"
  runtimeOnly 'com.github.ben-manes.caffeine:caffeine'
  runtimeOnly 'com.h2database:h2'
  runtimeOnly 'com.mysql:mysql-connector-j'
  runtimeOnly 'org.postgresql:postgresql'

  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.boot:spring-boot-testcontainers'
  testImplementation 'org.springframework.boot:spring-boot-docker-compose'
  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'org.testcontainers:mysql'
  
  // AWS SDK deps
  implementation 'software.amazon.awssdk:dsql:2.41.23'
  implementation 'software.amazon.dsql:aurora-dsql-hibernate-dialect:1.0.1'
  implementation 'software.amazon.dsql:aurora-dsql-jdbc-connector:1.3.0'

  implementation fileTree(dir: 'lib', includes: ['*.jar'])
}

tasks.named('test') {
  useJUnitPlatform()
  
  def profile = System.getProperty('spring.profiles.active') ?: System.getenv('SPRING_PROFILES_ACTIVE') ?: ''
  
  doFirst {
    if (!profile) {
      throw new GradleException("spring.profiles.active must be set. Use -Dspring.profiles.active=h2 or =dsql")
    }
  }
  
  systemProperty 'spring.profiles.active', profile
  environment 'CLUSTER_ENDPOINT', System.getenv('CLUSTER_ENDPOINT') ?: ''
  environment 'CLUSTER_USER', System.getenv('CLUSTER_USER') ?: ''
}
